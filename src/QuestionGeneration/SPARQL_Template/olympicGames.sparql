#title: Gold medal winners in the Olympic Games by age 
SELECT DISTINCT ?winner ?winnerLabel ?sport ?sportLabel ?eventTime ?age WHERE {
  ?winner wdt:P31 wd:Q5;
          wdt:P569 ?dob.
  MINUS { ?winner p:P569/psv:P569 [
                wikibase:timePrecision "7"^^xsd:integer ; # Century
                wikibase:timeValue ?dob] } # Meant to remove those born "19th century"
  FILTER (! wikibase:isSomeValue(?dob) ) # Removes those with unknown birth date
  
  ?sport wdt:P1346 ?winner ;
         wdt:P31 wd:Q18536594.
  
  # Work-around due to inconsistent modeling of the time of events 
  OPTIONAL { ?sport wdt:P585 ?eventPoint }
  OPTIONAL { ?sport wdt:P582 ?eventEnd }
  OPTIONAL { ?sport wdt:P361/wdt:P585 ?olympicPoint }
  BIND(COALESCE(?eventEnd,?eventPoint,?olympicPoint) AS ?eventTime)
  
  # Quick and dirty way to calculate age
  BIND ((ROUND(((?eventTime - ?dob)/3.6524))/100) AS ?age )
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
} ORDER BY ?age
